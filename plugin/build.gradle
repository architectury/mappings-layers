plugins {
    id "java-gradle-plugin"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

configurations {
    shadowGood
}

dependencies {
    implementation gradleApi()
    compileOnly project(":core")
    shadowGood project(path: ":core", configuration: "dev")
}

gradlePlugin {
    plugins {
        fabricLoom {
            id = "me.shedaniel.mappings-layers-plugin"
            implementationClass = "me.shedaniel.mappingslayers.MappingsLayersPlugin"
        }
    }
}

jar {
    classifier "bad"
}

shadowJar {
    configurations = [project.configurations.shadowGood]
    classifier null
}

build.dependsOn shadowJar

import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node

publishing {
    publications {
        maven(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName

            from components.java
            artifact shadowJar
        }

        plugin(MavenPublication) {
            groupId "me.shedaniel.mappings-layers-plugin"
            artifactId "me.shedaniel.mappings-layers-plugin.gradle.plugin"

            from components.java
            artifact shadowJar
        }

        mavenSnapshot(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version baseVersion + "-SNAPSHOT"

            from components.java
            artifact shadowJar
        }

        pluginSnapshot(MavenPublication) { publication ->
            groupId "me.shedaniel.mappings-layers-plugin"
            artifactId "me.shedaniel.mappings-layers-plugin.gradle.plugin"
            version baseVersion + "-SNAPSHOT"

            pom.withXml {
                // Based off org.gradle.plugin.devel.plugins.MavenPluginPublishPlugin
                Element root = asElement()
                Document document = root.getOwnerDocument()
                Node dependencies = root.appendChild(document.createElement('dependencies'))
                Node dependency = dependencies.appendChild(document.createElement('dependency'))
                Node groupId = dependency.appendChild(document.createElement('groupId'))
                groupId.setTextContent(project.group)
                Node artifactId = dependency.appendChild(document.createElement('artifactId'))
                artifactId.setTextContent(project.archivesBaseName)
                Node version = dependency.appendChild(document.createElement('version'))
                version.setTextContent(baseVersion + "-SNAPSHOT")
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
