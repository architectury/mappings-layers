plugins {
    id "java-gradle-plugin"
}

dependencies {
    implementation gradleApi()
    implementation project(":core")
}

gradlePlugin {
    automatedPublishing = false
    plugins {
        fabricLoom {
            id = "dev.architectury.mappings-layers-plugin"
            implementationClass = "dev.architectury.mappingslayers.MappingsLayersPlugin"
        }
    }
}

import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName

            from components.java
        }

        plugin(MavenPublication) {
            groupId "dev.architectury.mappings-layers-plugin"
            artifactId "dev.architectury.mappings-layers-plugin.gradle.plugin"

            from components.java
        }

        mavenSnapshot(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version baseVersion + "-SNAPSHOT"

            from components.java
        }

        pluginSnapshot(MavenPublication) {
            groupId "dev.architectury.mappings-layers-plugin"
            artifactId "dev.architectury.mappings-layers-plugin.gradle.plugin"
            version baseVersion + "-SNAPSHOT"

            pom.withXml {
                // Based off org.gradle.plugin.devel.plugins.MavenPluginPublishPlugin
                Element root = asElement()
                Document document = root.getOwnerDocument()
                Node dependencies = root.appendChild(document.createElement('dependencies'))
                Node dependency = dependencies.appendChild(document.createElement('dependency'))
                Node groupId = dependency.appendChild(document.createElement('groupId'))
                groupId.setTextContent(project.group)
                Node artifactId = dependency.appendChild(document.createElement('artifactId'))
                artifactId.setTextContent(project.archivesBaseName)
                Node version = dependency.appendChild(document.createElement('version'))
                version.setTextContent(baseVersion + "-SNAPSHOT")
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
